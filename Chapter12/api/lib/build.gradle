plugins {
    id 'java'
    id 'maven-publish'
    id "com.google.protobuf" version "0.9.2"
}

repositories {
    mavenCentral()
}

group = 'com.packt.modern.api'
version = '0.0.1'
sourceCompatibility = JavaVersion.VERSION_17

def grpcVersion = '1.54.1'
dependencies {
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-netty:${grpcVersion}"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

tasks.named('test') {
    useJUnitPlatform()
}

protobuf {
    clean {
        // Add cleaning directories here.
        // delete "$projectDir/src/java"
    }
    protoc {
        artifact = "com.google.protobuf:protoc:3.22.2"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.54.1"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        proto {
            // In addition to the default "src/main/proto"
            srcDir "src/main/grpc"
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = "sources"
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'payment-gateway-api'
            from components.java
        }
    }
}
